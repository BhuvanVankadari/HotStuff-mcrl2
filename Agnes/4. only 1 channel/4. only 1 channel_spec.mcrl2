% Sending a message through channel
% only 1 channel - fixed error by putting a bound on i,j,k

% sort Message_type = struct newView | prepare | preCommit | commit | decide;

% sort Message = struct message(type: Message_type, viewNum: Pos);

%n = 3f + 1
map n: Nat; % number of replicas
map f: Nat; %number of faulty replicas
map num_views: Pos; % number of views
 
eqn n = 1;
eqn f = 0;
eqn num_views = 2;

% calculate maximum element in a list
map maxList: List(Nat) -> Nat;

var a: Nat;
	list: List(Nat);

eqn maxList([]) = 0; % empty list
	maxList([a]) = a; % one element
	maxList(a |> list) = max(a, maxList(list)); % more than one element

act send_message, receive_message,receive_message_replica, send_message_leader, send_broadcast, receive_broadcast,message_sent, message_received, broadcast_sent, broadcast_received,receive_message_leader, send_message_replica: Nat # Nat # Nat; % message, sender, receiver
act transition;

%state = 0 -> replica sends message, state = 1 replica waits for message 
%replica should send only 1 msj
proc Replica(id: Nat, currentView: Nat, state: Bool) = 
	%replica sends message=
	(state == false ) -> (sum i: Nat.(  
     send_message(currentView,id,i).Replica(id,currentView,true) )) + 
	%replica receives message
	(state == true)  -> (sum j,k: Nat.(receive_broadcast(k,j,id).Replica(id,currentView,false)));


proc Leader(id : Nat, highestView : List(Nat), count_msj : Nat, broadcasts: Nat) = 
	 (count_msj < n - f) -> (sum i,j: Nat. (receive_message(i,j,id).Leader(id, highestView <| i, count_msj + 1, 0))) + 
	 (count_msj >= n - f && broadcasts < n)  -> (sum i: Nat. (send_broadcast(maxList(highestView), id, i).Leader(id,highestView, count_msj, broadcasts + 1)));

proc ChannelSend =
	(sum k,i,j: Nat.
	(i <= n && j <= n && k <= num_views) -> (receive_message_replica(k,i,j).send_message_leader(k,i,j).ChannelSend) + transition.ChannelReceive);

proc ChannelReceive = 
	 (sum k,i,j: Nat.
	 (i <= n && j <= n && k <= num_views) -> (receive_message_leader(k,j,i).send_message_replica(k,j,i).ChannelReceive) + transition.ChannelSend);

proc Channel = ChannelSend || ChannelReceive;

init allow({message_sent, message_received, broadcast_sent, broadcast_received, transition},
	 comm({ send_message | receive_message_replica -> message_sent,
		    send_message_leader | receive_message -> message_received,
			send_broadcast | receive_message_leader -> broadcast_sent,
			send_message_replica | receive_broadcast -> broadcast_received},
			Replica(1,1,false) || Leader(1,[],0,0) || Channel
	 ));


	